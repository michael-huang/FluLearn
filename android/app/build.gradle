def gradleProperties = new Properties()
def gradlePropertiesFile = rootProject.file('gradle.properties')
if (gradlePropertiesFile.exists()) {
    gradlePropertiesFile.withReader('UTF-8') { reader ->
        gradleProperties.load(reader)
    }
}

def localProperties = new Properties()
def localPropertiesFile = rootProject.file('local.properties')
if (localPropertiesFile.exists()) {
    localPropertiesFile.withReader('UTF-8') { reader ->
        localProperties.load(reader)
    }
}
def flutterRoot = localProperties.getProperty('flutter.sdk')
if (flutterRoot == null) {
    throw new Error("Flutter SDK not found. Define location with flutter.sdk in the local.properties file.")
}

apply plugin: 'com.android.application'
apply from: "$flutterRoot/packages/flutter_tools/gradle/flutter.gradle"

android {
    compileSdkVersion Integer.parseInt(gradleProperties.getProperty("compileSdkVersion"))
    buildToolsVersion gradleProperties.getProperty("buildToolsVersion")

    defaultConfig {
        minSdkVersion Integer.parseInt(gradleProperties.getProperty("minSdkVersion"))
        targetSdkVersion Integer.parseInt(gradleProperties.getProperty("targetSdkVersion"))
        versionCode gradleProperties.getProperty('appVersionCode').toInteger()
        versionName gradleProperties.getProperty('appVersionName')
    }

    buildTypes {
        release {
            //是否zip优化
            zipAlignEnabled true
            //是否移除无用的资源：工具栏->Refactor->Remove Unused Resources
            shrinkResources true
            //是否混淆
            minifyEnabled true
            //混淆配置文件
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            // TODO: Add your own signing config for the release build.
            // Signing with the debug keys for now, so `flutter run --release` works.
            signingConfig signingConfigs.debug
        }
    }

    lintOptions {
        // 如果构建时发现了致命（Fatal）的问题，会中止构建（具体由 abortOnError 控制）
        checkReleaseBuilds false
        // 设置为 true，则当 Lint 发现错误时停止 Gradle 构建
        abortOnError false
        disable 'InvalidPackage'
    }

    packagingOptions {
        doNotStrip '*/mips/*.so'
        doNotStrip '*/mips64/*.so'
        exclude 'META-INF/DEPENDENCIES.txt'
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/NOTICE.txt'
        exclude 'META-INF/NOTICE'
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/DEPENDENCIES'
        exclude 'META-INF/notice.txt'
        exclude 'META-INF/license.txt'
        exclude 'META-INF/dependencies.txt'
        exclude 'META-INF/LGPL*'
        exclude 'META-INF/ASL*'
        exclude 'META-INF/proguard/*'
        exclude 'META-INF/*.version'
        exclude 'LICENSE*'
        exclude 'junit/*/*'
    }

    externalNativeBuild {
        cmake {
            path file('CMakeLists.txt')
        }
    }

}

flutter {
    source '../..'
}

dependencies {
    implementation "androidx.core:core:$androidxCoreVersion"
    implementation "androidx.lifecycle:lifecycle-common:$androidxLifecycleVersion"
    implementation "androidx.annotation:annotation:$androidxCoreVersion"
    implementation "androidx.fragment:fragment:$androidxCoreVersion"
}
